(ns hooks.com.github.seancorfield.expectations
  (:require [clj-kondo.hooks-api :as api]))

(defn more-> [{:keys [node]}]
  (let [tail (rest (:children node))
        rewritten
        (api/list-node
         (list*
          (api/token-node 'cond->)
          (api/token-node 'nil)
          tail))]
    {:node rewritten}))

(defn more-of [{:keys [node]}]
  (let [bindings (fnext (:children node))
        pairs (partition 2 (nnext (:children node)))
        rewritten
        (api/list-node
         (list*
          (api/token-node 'let)
          (api/vector-node (vector bindings (api/token-node 'nil)))
          (map (fn [[e a]]
                 (api/list-node
                  (list
                   (api/token-node '=?)
                   e
                   a)))
               pairs)))]
    {:node rewritten}))
